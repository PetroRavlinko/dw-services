plugins {
    id "java"
    id "scala"
    id "war"
    id 'com.jfrog.bintray' version "1.7.1"
    id 'maven-publish'
    id 'jacoco'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    scalaMajorVersion = '2.11'
    scalaVersion = "${scalaMajorVersion}.5"
}

def travisJobId = System.getenv("TRAVIS_BUILD_NUMBER")
String dwVersion = System.getenv("DW_VERSION")

group = 'com.dw.services'
version = "$dwVersion.$travisJobId"

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.springframework:spring-webmvc:4.1.6.RELEASE'
    compile 'javax.servlet:jstl:1.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    compile 'io.rest-assured:json-path:3.0.1'
    compile 'io.rest-assured:xml-path:3.0.1'

    compile "org.scala-lang:scala-library:${scalaVersion}"
    compile "org.scala-lang.modules:scala-xml_${scalaMajorVersion}:1.0.3"

    testCompile 'junit:junit:4.12'
    testCompile 'io.rest-assured:rest-assured:3.0.1'
    testCompile 'io.rest-assured:json-schema-validator:3.0.1'
    testCompile 'io.rest-assured:scala-support:3.0.1'
    testCompile 'org.scalatest:scalatest_2.11:3.0.0'
    testRuntime 'org.pegdown:pegdown:1.6.0'
}

configurations {
    e2eCompile.extendsFrom testCompile
    e2eRuntime.extendsFrom testRuntime
}

sourceSets {
    e2e {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDir file('src/e2e/java')
    }

    test {
        resources.srcDir file('src/e2e/resources')
    }
}

test {
    jacoco {
        destinationFile = file("build/jacoco/test.exec")
    }
}

task e2e(type: Test) {
    testClassesDir = sourceSets.e2e.output.classesDir
    classpath = sourceSets.e2e.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination "build/jacoco/jacocoHtml"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

repositories {
    repositories {
        jcenter()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    publish = true
    override = true

    filesSpec {
        from 'build/libs'
        into "/"
    }

    pkg {
        repo = 'snapshots'
        name = 'dw-services'

        issueTrackerUrl = 'https://github.com/PetroRavlinko/dw-services/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/PetroRavlinko/dw-services.git'
        publicDownloadNumbers = true
        githubRepo = 'PetroRavlinko/dw-services'
        githubReleaseNotesFile = 'README.md'
        version {
            name = "$dwVersion.$travisJobId"
            vcsTag = "$dwVersion.$travisJobId"
        }
    }
}